{ config, lib, ... }:

let
  cfg = config.ctrl-os.hardware.devices.nvidia-jetson-orin-nano-super;
in
{
  options = {
    ctrl-os.hardware.devices.nvidia-jetson-orin-nano-super = {
      enableOotModules = lib.mkEnableOption "the NVIDIA Out-Of-Tree kernel modules" // {
        default = true;
      };
    };
  };

  config = lib.mkIf cfg.enable {
    nixpkgs.hostPlatform = "aarch64-linux";

    boot.initrd.availableKernelModules = [
      # Enable PCIe support at boot time
      "phy_tegra194_p2u"
      "pcie_tegra194"
      # Enable USB support for USB Boot
      "xhci-tegra"
      "phy-tegra-xusb"
    ];

    boot.extraModulePackages = lib.mkMerge [
      (lib.mkIf cfg.enableOotModules [
        config.boot.kernelPackages.nvidia-oot
      ])
    ];

    nixpkgs.overlays = [
      (final: super: {
        # This is ugly, but needed to *properly* overlay `linuxPackages` packages.
        linuxKernel = super.linuxKernel // {
          # Thanks to laziness, we only need to override `packagesFor`.
          packagesFor =
            kernel:
            let
              # From which we apply the previous version of the function
              packages = super.linuxKernel.packagesFor kernel;
            in
            # And merge our package in.
            packages
            // {
              # Without forgetting to `callPackage` from this kernel's package set!
              nvidia-oot = packages.callPackage ./nvidia-oot { };
            };
        };
      })
    ];
  };
}
